plugins {
    id 'dev.huskuraft.universal' version '0.6.0'
}

import java.nio.file.Files

static def extract(File file, String version) {
    def text = Files.readString(file.toPath())
    def split = text.split('----------\n\n')
    for (final def log in split) {
        if (log.startsWith("### $version")) {
            return log.replaceFirst("### $version", '').trim()
        }
    }
    throw new IllegalStateException("$version not found in changelog")
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {
    implementation 'com.google.code.findbugs:jsr305:3.0.2'

    annotationProcessor 'com.google.auto.service:auto-service:1.1.1'
    compileOnly 'com.google.auto.service:auto-service:1.1.1'

    compileOnly 'io.netty:netty-all:4.1.109.Final'
    compileOnly 'org.slf4j:slf4j-api:2.0.13'

    implementation 'dev.huskuraft.universal:common-api:0.2.0'
}

universal {
    changelog = extract(rootProject.file('CHANGELOG.md'), project.version as String)
    changelogFormat = 'markdown'

    modrinth {
        token = System.getenv('MODRINTH_TOKEN')
    }
    curseforge {
        token = System.getenv('CURSEFORGE_TOKEN')
    }
    targets = [
        ['1.17.1']                  : ['fabric-api', 'quilt-api', 'forge-api'],
        ['1.18.1', '1.18']          : ['fabric-api', 'quilt-api', 'forge-api'],
        ['1.18.2']                  : ['fabric-api', 'quilt-api', 'forge-api'],
        ['1.19.2', '1.19.1', '1.19']: ['fabric-api', 'quilt-api', 'forge-api'],
        ['1.19.3']                  : ['fabric-api', 'quilt-api', 'forge-api'],
        ['1.19.4']                  : ['fabric-api', 'quilt-api', 'forge-api'],
        ['1.20.1', '1.20']          : ['fabric-api', 'quilt-api', 'forge-api'],
        ['1.20.2', '1.20.1']        : ['fabric-api', 'quilt-api', 'forge-api'],
        ['1.20.4', '1.20.3']        : ['fabric-api', 'quilt-api', 'forge-api'],
        ['1.20.6', '1.20.5']        : ['fabric-api', 'quilt-api', 'forge-api', 'neoforge-api'],
        ['1.21.1', '1.21']          : ['fabric-api', 'quilt-api', 'forge-api', 'neoforge-api'],
        ['1.21.3']                  : ['fabric-api', 'quilt-api', 'forge-api', 'neoforge-api'],
    ]
}
